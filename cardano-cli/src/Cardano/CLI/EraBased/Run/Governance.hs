{-# LANGUAGE DataKinds #-}
{-# LANGUAGE GADTs #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE NamedFieldPuns #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE TypeApplications #-}

module Cardano.CLI.EraBased.Run.Governance
  ( GovernanceCmdError(..)
  , runGovernanceCmd
  , runGovernanceDelegationCertificate
  , runGovernanceMIRCertificatePayStakeAddrs
  , runGovernanceMIRCertificateTransfer
  , runGovernanceGenesisKeyDelegationCertificate
  ) where

import           Cardano.Api
import qualified Cardano.Api.Ledger as Ledger
import           Cardano.Api.Shelley
import qualified Cardano.Api.Shelley as Api

import           Cardano.Binary (DecoderError)
import           Cardano.CLI.EraBased.Constraints
import           Cardano.CLI.EraBased.Options.Governance
import           Cardano.CLI.EraBased.Run.StakeAddress
import           Cardano.CLI.Run.Legacy.Read (CddlError, fileOrPipe, readFileTx)
import           Cardano.CLI.Types.Common
import           Cardano.CLI.Types.Governance
import qualified Cardano.CLI.Types.Governance as Cli
import           Cardano.CLI.Types.Key
import           Cardano.CLI.Types.Legacy
import qualified Cardano.Ledger.Shelley.TxBody as Shelley

import           Control.Monad
import           Control.Monad.IO.Class
import           Control.Monad.Trans.Class
import           Control.Monad.Trans.Except (ExceptT)
import           Control.Monad.Trans.Except.Extra
import           Data.Aeson (eitherDecode)
import           Data.Bifunctor
import qualified Data.ByteString as BS
import qualified Data.ByteString.Char8 as BSC
import qualified Data.ByteString.Lazy as LB
import           Data.Function ((&))
import qualified Data.Map.Strict as Map
import           Data.String (fromString)
import           Data.Text (Text)
import qualified Data.Text as Text
import qualified Data.Text.Encoding as Text
import           Data.Text.Encoding.Error
import qualified Data.Text.IO as Text
import qualified Data.Text.Read as Text
import qualified System.IO as IO
import           System.IO (stderr, stdin, stdout)

data GovernanceCmdError
  = -- Voting related
    GovernanceCmdStakeCredError StakeAddressCmdError
  | GovernanceCmdVotingCredentialDecodeError DecoderError
  | GovernanceCmdReadFileError (FileError InputDecodeError)
    -- Governance action related
  | GovernanceCmdExpectedStakeKeyCredentialError
  | GovernanceCmdNonUtf8EncodedConstitutionError UnicodeException

  | GovernanceCmdTextEnvReadError !(FileError TextEnvelopeError)
  | GovernanceCmdCddlError !CddlError
  | GovernanceCmdKeyReadError !(FileError InputDecodeError)
  | GovernanceCmdCostModelReadError !(FileError ())
  | GovernanceCmdTextEnvWriteError !(FileError ())
  | GovernanceCmdEmptyUpdateProposalError
  | GovernanceCmdMIRCertificateKeyRewardMistmachError
      !FilePath
      !Int
      -- ^ Number of stake verification keys
      !Int
      -- ^ Number of reward amounts
  | GovernanceCmdCostModelsJsonDecodeError !FilePath !Text
  | GovernanceCmdEmptyCostModelError !FilePath
  | GovernanceCmdUnexpectedKeyTypeError
      ![TextEnvelopeType]
      -- ^ Expected key types
  | GovernanceCmdPollOutOfBoundAnswerError
      !Int
      -- ^ Maximum answer index
  | GovernanceCmdPollInvalidChoiceError
  | GovernanceCmdDecoderError !DecoderError
  | GovernanceCmdVerifyPollError !GovernancePollError
  | GovernanceCmdWriteFileError !(FileError ())
  | GovernanceCmdDelegReadError !(FileError InputDecodeError)
  | GovernanceCmdCredentialError !StakeAddressCmdError -- TODO: Refactor. We shouldn't be using legacy error types
  | GovernanceCmdCertificateWriteFileError !(FileError ())
  | GovernanceCmdDelegationGenericError -- TODO Delete and replace with more specific errors
  deriving Show

runGovernanceCmd :: ()
  => Ledger.EraCrypto (ShelleyLedgerEra era) ~ Ledger.StandardCrypto
  => GovernanceCmd era
  -> ExceptT GovernanceCmdError IO ()
runGovernanceCmd = \case
  GovernanceMIRPayStakeAddressesCertificate w mirpot vKeys rewards out ->
    runGovernanceMIRCertificatePayStakeAddrs w mirpot vKeys rewards out
  GovernanceMIRTransfer w ll oFp direction ->
    runGovernanceMIRCertificateTransfer w ll oFp direction
  GovernanceDelegationCertificateCmd stakeIdentifier delegationTarget outFp ->
    runGovernanceDelegationCertificate stakeIdentifier delegationTarget outFp
  GovernanceGenesisKeyDelegationCertificate w genVk genDelegVk vrfVk out ->
    runGovernanceGenesisKeyDelegationCertificate w genVk genDelegVk vrfVk out
  GovernanceVoteCmd (CreateVoteCmd (ConwayVote sbe voteChoice voteType govActTcIn voteStakeCred fp)) ->
    runGovernanceCreateVoteCmd sbe voteChoice voteType govActTcIn voteStakeCred fp
  GovernanceActionCmd (CreateConstitution (Cli.NewConstitution sbe deposit voteStakeCred newconstitution fp)) ->
    runGovernanceNewConstitutionCmd sbe deposit voteStakeCred newconstitution fp
  GovernanceUpdateProposal out eNo genVKeys ppUp mCostModelFp ->
    runGovernanceUpdateProposal out eNo genVKeys ppUp mCostModelFp
  GovernanceCreatePoll prompt choices nonce out ->
    runGovernanceCreatePoll prompt choices nonce out
  GovernanceAnswerPoll poll ix mOutFile ->
    runGovernanceAnswerPoll poll ix mOutFile
  GovernanceVerifyPoll poll metadata mOutFile ->
    runGovernanceVerifyPoll poll metadata mOutFile

runGovernanceUpdateProposal
  :: File () Out
  -> EpochNo
  -> [VerificationKeyFile In]
  -- ^ Genesis verification keys
  -> ProtocolParametersUpdate
  -> Maybe FilePath -- ^ Cost models file path
  -> ExceptT GovernanceCmdError IO ()
runGovernanceUpdateProposal upFile eNo genVerKeyFiles upPprams mCostModelFp = do
  finalUpPprams <- case mCostModelFp of
    Nothing -> return upPprams
    Just fp -> do
      costModelsBs <- handleIOExceptT (GovernanceCmdCostModelReadError . FileIOError fp) $ LB.readFile fp

      cModels <- pure (eitherDecode costModelsBs)
        & onLeft (left . GovernanceCmdCostModelsJsonDecodeError fp . Text.pack)

      let costModels = fromAlonzoCostModels cModels

      when (null costModels) $ left (GovernanceCmdEmptyCostModelError fp)

      return $ upPprams {protocolUpdateCostModels = costModels}

  when (finalUpPprams == mempty) $ left GovernanceCmdEmptyUpdateProposalError

  genVKeys <- sequence
    [ firstExceptT GovernanceCmdTextEnvReadError . newExceptT $ readFileTextEnvelope (AsVerificationKey AsGenesisKey) vkeyFile
    | vkeyFile <- genVerKeyFiles
    ]
  let genKeyHashes = fmap verificationKeyHash genVKeys
      upProp = makeShelleyUpdateProposal finalUpPprams genKeyHashes eNo

  firstExceptT GovernanceCmdTextEnvWriteError . newExceptT
    $ writeLazyByteStringFile upFile $ textEnvelopeToJSON Nothing upProp

runGovernanceCreatePoll
  :: Text
  -> [Text]
  -> Maybe Word
  -> File GovernancePoll Out
  -> ExceptT GovernanceCmdError IO ()
runGovernanceCreatePoll govPollQuestion govPollAnswers govPollNonce out = do
  let poll = GovernancePoll{ govPollQuestion, govPollAnswers, govPollNonce }

  let description = fromString $ "An on-chain poll for SPOs: " <> Text.unpack govPollQuestion
  firstExceptT GovernanceCmdTextEnvWriteError . newExceptT $
    writeFileTextEnvelope out (Just description) poll

  let metadata = asTxMetadata poll
        & metadataToJson TxMetadataJsonDetailedSchema

  let outPath = unFile out & Text.encodeUtf8 . Text.pack

  liftIO $ do
    BSC.hPutStrLn stderr $ mconcat
      [ "Poll created successfully.\n"
      , "Please submit a transaction using the resulting metadata.\n"
      ]
    BSC.hPutStrLn stdout (prettyPrintJSON metadata)
    BSC.hPutStrLn stderr $ mconcat
      [ "\n"
      , "Hint (1): Use '--json-metadata-detailed-schema' and '--metadata-json-file' "
      , "from the build or build-raw commands.\n"
      , "Hint (2): You can redirect the standard output of this command to a JSON "
      , "file to capture metadata.\n\n"
      , "Note: A serialized version of the poll suitable for sharing with "
      , "participants has been generated at '" <> outPath <> "'."
      ]

runGovernanceAnswerPoll
  :: File GovernancePoll In
  -> Maybe Word -- ^ Answer index
  -> Maybe (File () Out) -- ^ Output file
  -> ExceptT GovernanceCmdError IO ()
runGovernanceAnswerPoll pollFile maybeChoice mOutFile = do
  poll <- firstExceptT GovernanceCmdTextEnvReadError . newExceptT $
    readFileTextEnvelope AsGovernancePoll pollFile

  choice <- case maybeChoice of
    Nothing -> do
      askInteractively poll
    Just ix -> do
      validateChoice poll ix
      liftIO $ BSC.hPutStrLn stderr $ Text.encodeUtf8 $ Text.intercalate "\n"
        [ govPollQuestion poll
        , "â†’ " <> (govPollAnswers poll !! fromIntegral ix)
        , ""
        ]
      pure ix

  let pollAnswer = GovernancePollAnswer
        { govAnsPoll = hashGovernancePoll poll
        , govAnsChoice = choice
        }
  let metadata =
        metadataToJson TxMetadataJsonDetailedSchema (asTxMetadata pollAnswer)

  liftIO $ BSC.hPutStrLn stderr $ mconcat
      [ "Poll answer created successfully.\n"
      , "Please submit a transaction using the resulting metadata.\n"
      , "To be valid, the transaction must also be signed using a valid key\n"
      , "identifying your stake pool (e.g. your cold key).\n"
      ]

  lift (writeByteStringOutput mOutFile (prettyPrintJSON metadata))
    & onLeft (left . GovernanceCmdWriteFileError)

  liftIO $ BSC.hPutStrLn stderr $ mconcat
      [ "\n"
      , "Hint (1): Use '--json-metadata-detailed-schema' and '--metadata-json-file' "
      , "from the build or build-raw commands.\n"
      , "Hint (2): You can redirect the standard output of this command to a JSON "
      , "file to capture metadata."
      ]
 where
  validateChoice :: GovernancePoll -> Word -> ExceptT GovernanceCmdError IO ()
  validateChoice GovernancePoll{govPollAnswers} ix = do
    let maxAnswerIndex = length govPollAnswers - 1
    when (fromIntegral ix > maxAnswerIndex) $ left $
      GovernanceCmdPollOutOfBoundAnswerError maxAnswerIndex

  askInteractively :: GovernancePoll -> ExceptT GovernanceCmdError IO Word
  askInteractively poll@GovernancePoll{govPollQuestion, govPollAnswers} = do
    liftIO $ BSC.hPutStrLn stderr $ Text.encodeUtf8 $ Text.intercalate "\n"
      ( govPollQuestion
      : [ "[" <> textShow ix <> "] " <> answer
        | (ix :: Int, answer) <- zip [0..] govPollAnswers
        ]
      )
    liftIO $ BSC.hPutStrLn stderr ""
    liftIO $ BSC.hPutStr stderr "Please indicate an answer (by index): "
    txt <- liftIO $ Text.hGetLine stdin
    liftIO $ BSC.hPutStrLn stderr ""
    case Text.decimal txt of
      Right (choice, rest) | Text.null rest ->
        choice <$ validateChoice poll choice
      _ ->
        left GovernanceCmdPollInvalidChoiceError

runGovernanceVerifyPoll
  :: File GovernancePoll In
  -> File (Api.Tx ()) In
  -> Maybe (File () Out) -- ^ Output file
  -> ExceptT GovernanceCmdError IO ()
runGovernanceVerifyPoll pollFile txFile mOutFile = do
  poll <- firstExceptT GovernanceCmdTextEnvReadError . newExceptT $
    readFileTextEnvelope AsGovernancePoll pollFile

  txFileOrPipe <- liftIO $ fileOrPipe (unFile txFile)
  tx <- firstExceptT GovernanceCmdCddlError . newExceptT $
    readFileTx txFileOrPipe

  signatories <- firstExceptT GovernanceCmdVerifyPollError . newExceptT $ pure $
    verifyPollAnswer poll tx

  liftIO $ IO.hPutStrLn stderr $ "Found valid poll answer with " <> show (length signatories) <> " signatories"

  lift (writeByteStringOutput mOutFile (prettyPrintJSON signatories))
    & onLeft (left . GovernanceCmdWriteFileError)

runGovernanceCreateVoteCmd
  :: ShelleyBasedEra era
  -> Vote
  -> VType
  -> TxIn
  -> VerificationKeyOrFile StakePoolKey
  -> File (ConwayVote era) Out
  -> ExceptT GovernanceCmdError IO ()
runGovernanceCreateVoteCmd sbe vChoice vType govActionTxIn votingStakeCred oFp =
  obtainEraConstraints sbe $ do
    vStakePoolKey <- firstExceptT GovernanceCmdReadFileError . newExceptT $ readVerificationKeyOrFile AsStakePoolKey votingStakeCred
    let stakePoolKeyHash = verificationKeyHash vStakePoolKey
        vStakeCred = StakeCredentialByKey . (verificationKeyHash . castVerificationKey) $ vStakePoolKey
    case vType of
      VCC -> do
        votingCred <- hoistEither $ first GovernanceCmdVotingCredentialDecodeError $ toVotingCredential sbe vStakeCred
        let govActIdentifier = makeGoveranceActionId sbe govActionTxIn
            voteProcedure = createVotingProcedure sbe vChoice (VoterCommittee votingCred) govActIdentifier
        firstExceptT GovernanceCmdWriteFileError . newExceptT $ obtainEraPParamsConstraint sbe $ writeFileTextEnvelope oFp Nothing voteProcedure

      VDR -> do
        votingCred <- hoistEither $ first GovernanceCmdVotingCredentialDecodeError $ toVotingCredential sbe vStakeCred
        let govActIdentifier = makeGoveranceActionId sbe govActionTxIn
            voteProcedure = createVotingProcedure sbe vChoice (VoterDRep votingCred) govActIdentifier
        firstExceptT GovernanceCmdWriteFileError . newExceptT $ obtainEraPParamsConstraint sbe $ writeFileTextEnvelope oFp Nothing voteProcedure

      VSP -> do
        let govActIdentifier = makeGoveranceActionId sbe govActionTxIn
            voteProcedure = createVotingProcedure sbe vChoice (VoterSpo stakePoolKeyHash) govActIdentifier
        firstExceptT GovernanceCmdWriteFileError . newExceptT $ obtainEraPParamsConstraint sbe $ writeFileTextEnvelope oFp Nothing voteProcedure

runGovernanceNewConstitutionCmd
  :: AnyShelleyBasedEra
  -> Lovelace
  -> VerificationKeyOrFile StakePoolKey
  -> Constitution
  -> NewConstitutionFile Out
  -> ExceptT GovernanceCmdError IO ()
runGovernanceNewConstitutionCmd sbe deposit stakeVoteCred constitution oFp = do
  vStakePoolKeyHash
    <- fmap (verificationKeyHash . castVerificationKey)
        <$> firstExceptT GovernanceCmdReadFileError . newExceptT
              $ readVerificationKeyOrFile AsStakePoolKey stakeVoteCred
  case constitution of
    ConstitutionFromFile fp  -> do
      cBs <- liftIO $ BS.readFile $ unFile fp
      _utf8EncodedText <- firstExceptT GovernanceCmdNonUtf8EncodedConstitutionError . hoistEither $ Text.decodeUtf8' cBs
      let govAct = ProposeNewConstitution cBs
      runGovernanceCreateActionCmd sbe deposit vStakePoolKeyHash govAct oFp

    ConstitutionFromText c -> do
      let constitBs = Text.encodeUtf8 c
          govAct = ProposeNewConstitution constitBs
      runGovernanceCreateActionCmd sbe deposit vStakePoolKeyHash govAct oFp

runGovernanceCreateActionCmd
  :: AnyShelleyBasedEra
  -> Lovelace
  -> Hash StakeKey
  -> GovernanceAction
  -> File a Out
  -> ExceptT GovernanceCmdError IO ()
runGovernanceCreateActionCmd anyEra deposit depositReturnAddr govAction oFp = do
  AnyShelleyBasedEra sbe <- pure anyEra
  let proposal = createProposalProcedure sbe deposit depositReturnAddr govAction

  firstExceptT GovernanceCmdWriteFileError . newExceptT
    $ obtainEraPParamsConstraint sbe
    $ writeFileTextEnvelope oFp Nothing proposal

runGovernanceDelegationCertificate
  :: StakeIdentifier
  -> AnyDelegationTarget
  -> File () Out
  -> ExceptT GovernanceCmdError IO ()
runGovernanceDelegationCertificate stakeIdentifier delegationTarget outFp = do
  stakeCred <-
    getStakeCredentialFromIdentifier stakeIdentifier
      & firstExceptT GovernanceCmdCredentialError

  case delegationTarget of
    ShelleyToBabbageDelegTarget sTob stakePool -> do
      poolId <- lift (readVerificationKeyOrHashOrFile AsStakePoolKey stakePool)
                  & onLeft (left . GovernanceCmdDelegReadError)
      let req = StakeDelegationRequirementsPreConway sTob stakeCred poolId
          delegCert = makeStakeAddressDelegationCertificate req
          description = Just @TextEnvelopeDescr "Stake Address Delegation Certificate"
      firstExceptT GovernanceCmdCertificateWriteFileError
        . newExceptT
        $ writeLazyByteStringFile outFp
        $ obtainIsShelleyBasedEraShelleyToBabbage sTob
        $ textEnvelopeToJSON description delegCert

    ConwayOnwardDelegTarget cOnwards target -> do
      delegatee <- toLedgerDelegatee target
      let req = StakeDelegationRequirementsConwayOnwards cOnwards stakeCred delegatee
          delegCert = makeStakeAddressDelegationCertificate req
          -- TODO: Conway era - update description to say if its delegating voting stake or "regular" stake
          description = Just @TextEnvelopeDescr "Stake Address Delegation Certificate"
      firstExceptT GovernanceCmdCertificateWriteFileError
        . newExceptT
        $ writeLazyByteStringFile outFp
        $ obtainIsShelleyBasedEraConwayOnwards cOnwards
        $ textEnvelopeToJSON description delegCert

toLedgerDelegatee
  :: StakeTarget era
  -> ExceptT GovernanceCmdError IO (Ledger.Delegatee (Ledger.EraCrypto (ShelleyLedgerEra era)))
toLedgerDelegatee t =
  case t of
    TargetStakePool cOnwards vk -> do
      StakePoolKeyHash kHash
        <- lift (readVerificationKeyOrHashOrFile AsStakePoolKey vk)
             & onLeft (left . GovernanceCmdDelegReadError)

      right $ Ledger.DelegStake $ obtainIsShelleyBasedEraConwayOnwards cOnwards kHash
    TargetVotingDrep _ -> error "TODO: Conway era - Ledger.DelegVote"
    TargetVotingDrepAndStakePool _ -> error "TODO: Conway era - Ledger.DelegStakeVote"




runGovernanceMIRCertificatePayStakeAddrs :: forall era. ()
  => Ledger.EraCrypto (ShelleyLedgerEra era) ~ Ledger.StandardCrypto
  => ShelleyToBabbageEra era
  -> Shelley.MIRPot
  -> [StakeAddress] -- ^ Stake addresses
  -> [Lovelace]     -- ^ Corresponding reward amounts (same length)
  -> File () Out
  -> ExceptT GovernanceCmdError IO ()
runGovernanceMIRCertificatePayStakeAddrs w mirPot sAddrs rwdAmts oFp =
  obtainIsShelleyBasedEraShelleyToBabbage w $ do
    unless (length sAddrs == length rwdAmts) $
      left GovernanceCmdDelegationGenericError
        -- TODO throw specific error:
        -- (GovernanceCmdMIRCertificateKeyRewardMistmachError)
        --       (unFile oFp) (length sAddrs) (length rwdAmts)

    let sCreds  = map stakeAddressCredential sAddrs
        mirTarget = Ledger.StakeAddressesMIR
                      $ Map.fromList [ (toShelleyStakeCredential scred, Ledger.toDeltaCoin (toShelleyLovelace rwdAmt))
                                      | (scred, rwdAmt) <- zip sCreds rwdAmts
                                      ]
        mirReq = MirCertificateRequirements w mirPot (obtainEraCryptoConstraints (shelleyBasedEra @era) mirTarget)
        mirCert = makeMIRCertificate mirReq

    firstExceptT (const GovernanceCmdDelegationGenericError)
      . newExceptT
      $ writeLazyByteStringFile oFp
      $ textEnvelopeToJSON (Just mirCertDesc) mirCert

  where
    mirCertDesc :: TextEnvelopeDescr
    mirCertDesc = "Move Instantaneous Rewards Certificate"


runGovernanceMIRCertificateTransfer :: forall era. ()
  => Ledger.EraCrypto (ShelleyLedgerEra era) ~ Ledger.StandardCrypto
  => ShelleyToBabbageEra era
  -> Lovelace
  -> File () Out
  -> TransferDirection
  -> ExceptT GovernanceCmdError IO ()
runGovernanceMIRCertificateTransfer w ll oFp direction =
  obtainIsShelleyBasedEraShelleyToBabbage w $ do
    let mirTarget = Ledger.SendToOppositePotMIR (toShelleyLovelace ll)
    let mirReq mirPot = MirCertificateRequirements w mirPot mirTarget

    mirCert <-
      case direction of
        TransferToReserves -> return $ makeMIRCertificate $ mirReq Ledger.TreasuryMIR
        TransferToTreasury -> return $ makeMIRCertificate $ mirReq Ledger.ReservesMIR

    firstExceptT (const GovernanceCmdDelegationGenericError)
      . newExceptT
      $ writeLazyByteStringFile oFp
      $ textEnvelopeToJSON (Just $ mirCertDesc direction) mirCert

    where
      mirCertDesc :: TransferDirection -> TextEnvelopeDescr
      mirCertDesc TransferToTreasury = "MIR Certificate Send To Treasury"
      mirCertDesc TransferToReserves = "MIR Certificate Send To Reserves"

runGovernanceGenesisKeyDelegationCertificate :: forall era. ()
  => ShelleyToBabbageEra era
  -> VerificationKeyOrHashOrFile GenesisKey
  -> VerificationKeyOrHashOrFile GenesisDelegateKey
  -> VerificationKeyOrHashOrFile VrfKey
  -> File () Out
  -> ExceptT GovernanceCmdError IO ()
runGovernanceGenesisKeyDelegationCertificate w genVkOrHashOrFp genDelVkOrHashOrFp vrfVkOrHashOrFp oFp =
  obtainIsShelleyBasedEraShelleyToBabbage w $ do
    genesisVkHash <- firstExceptT (const GovernanceCmdDelegationGenericError)
      . newExceptT
      $ readVerificationKeyOrHashOrTextEnvFile AsGenesisKey genVkOrHashOrFp
    genesisDelVkHash <-firstExceptT (const GovernanceCmdDelegationGenericError)
      . newExceptT
      $ readVerificationKeyOrHashOrTextEnvFile AsGenesisDelegateKey genDelVkOrHashOrFp
    vrfVkHash <- firstExceptT (const GovernanceCmdDelegationGenericError)
      . newExceptT
      $ readVerificationKeyOrHashOrFile AsVrfKey vrfVkOrHashOrFp

    let req = GenesisKeyDelegationRequirements w genesisVkHash genesisDelVkHash vrfVkHash
        genKeyDelegCert = makeGenesisKeyDelegationCertificate req

    firstExceptT (const GovernanceCmdDelegationGenericError)
      . newExceptT
      $ writeLazyByteStringFile oFp
      $ textEnvelopeToJSON (Just genKeyDelegCertDesc) genKeyDelegCert
  where
    genKeyDelegCertDesc :: TextEnvelopeDescr
    genKeyDelegCertDesc = "Genesis Key Delegation Certificate"
